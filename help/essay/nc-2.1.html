<h1>2. Sound Synthesis in SuperCollider</h1><h2>2.1 Subtractive and Additive Synthesis</h2><p>Note that sounds will at first be in mono, in the left ear. Later on we will sort out stereo position.</p><h3>Unit Generators</h3><p>SuperCollider follows the <em>Unit Generator</em> paradigm also used in other synthesis languages like Csound, Max/MSP, Pd, Reaktor and others.</p><p>There are many primitive building blocks, like types of tone generator, filter or spatialiser, that are the unit generators. These are connected together in a processing graph to make more complicated synthesisers and sound processors. These primitives are referred to as UGens.</p><p>Each UGen has some set of inputs and outputs. Most UGens have just one output, an audio stream or some sort of control signal. The inputs vary a lot depending on the function of the UGen.</p><p>You will get used to the typical parameter values expected as inputs or outputs as you learn about the different UGens.</p><p>There are certain ways to program connections which are part of the syntax of the SuperCollider language, and particular names for units that you will encounter as you learn this system.</p><h3>Subtractive Synthesis</h3><p>This is a good way to start learning SuperCollider.</p><p>In subtractive synthesis, we start with a complex source, and we subtract parts from this raw sound to make a more sculpted sound. This is also termed a source+filter model.</p><p>This line will make a pure white noise source, equal energy at all spectral frequencies; it can be unpleasant to listen to - the 0.1 makes sure its not too loud, but be careful playing this</p><pre><code>WhiteNoise() * 0.1
</code></pre><p>That was just the source alone. Now I have to filter it to make a less raw sound.</p><pre><code>LPF(WhiteNoise() * 0.1, 1000)
</code></pre><p>The LPF is a Low Pass Filter which tails off energy above its cutoff frequency, which is 1000Hz in this example.</p><p>In SuperCollider, to plug the white noise generator WhiteNoise into the filter LPF I nest one within the other. You can think of a UGen's inputs being the list of slots within the parentheses, <em>LPF(input signal, cutoff frequency, ... )</em>, and in the example above, the thing to plug into the input signal slot is a white noise source, so that's where the WhiteNoise generator goes. The cutoff frequency is a fixed number, 1000, the second argument.</p><p>Say that we now want a varying filter cutoff over time. One UGen we could use here is the line generator.  <em>Ln(10000, 1000, 10)</em> takes ten seconds to go from 10000 to 1000: inputs to <em>Ln</em> are start, end, duration.</p><p>So instead of the fixed value 1000, the Ln UGen goes in that second slot</p><pre><code>LPF(WhiteNoise() * 0.1, Ln(10000, 1000, 10))
</code></pre><p>Listen for ten seconds at least to hear the full effect.</p><p>There are lots of possible sources and lots of possible filters (try these help files).</p><p>Some example unite generators:</p><ul><li>Oscillators: [Saw] [Blip]</li><li>Noise Sources: [PinkNoise] [LFNoise0]</li><li>Filters: [HPF] [BPF] [Resonz]</li></ul><p>Example of plugging one source into a filter:</p><pre><code>Resonz(LFNoise0(400), 1000, 0.1)
</code></pre><p>Again using the line generator to change the filter centre frequency over time</p><pre><code>Resonz(LFNoise0(400), Ln(10000,1000,10), 0.1)
</code></pre><p>An explicit and neater way to write this (we'll come back to this formulation).</p><pre><code>var source = LFNoise0(400);
var line = Ln(10000, 1000, 10);
Resonz(source, line, 0.1)
</code></pre><h3>Additive Synthesis</h3><p>Rather than starting with something complex and taking energy away to sculpt a sound, we can start with simple building blocks and add many of them together to create more involved sounds</p><p>The classic building block in computer music is the sine tone</p><pre><code>SinOsc(440, 0) * 0.1 // Concert A (440Hz)
</code></pre><p>Here is one way to get two sine tones at once:</p><pre><code>SinOsc(400, 0) + SinOsc(660, 0) * 0.1
</code></pre><p>And here is a much easier way</p><pre><code>SinOsc([400, 660], 0) * 0.1
</code></pre><p>Something special just happened to the stereo field, and I'll explain this in a moment.</p><p>Let me first introduce a panning UGen, <em>Pan2(input signal, pan position, amplitude)</em>.</p><p>Pan position goes from -1 (hard left) to 1 (hard right).</p><pre><code>Pan2(WhiteNoise(), MouseX(-1, 1, 0, 0.2), 0.1)
</code></pre><p>So the panner takes a mono signal, and places it in the stereo field.</p><p>Now, multichannel sound is really straight forward to create in SuperCollider, just by using an array.</p><p>We'll look at arrays more closely in a later week, but for now just think of them as lists of data.</p><pre><code>[100,200,300,400,500] // 5 numbers in a list
</code></pre><p>Each successive element in the list will be placed on one channel.</p><p>One channel sound.</p><pre><code> SinOsc([400], 0) * 0.1
</code></pre><p>Also one channel sound - no array brackets are needed for a single number.</p><pre><code>SinOsc(400, 0) * 0.1
</code></pre><p>Two channel sound.</p><pre><code>SinOsc([400, 660], 0) * 0.1
</code></pre><p>Three channel sound - you may only hear two, because you probably have a stereo output on your computer.</p><pre><code>SinOsc([400, 660, 870], 0) * 0.1
</code></pre><p>We need a way to take multiple channels of sound and turn them into a mono or stereo signal.</p><p>One method is to <em>sum</em> the multichannel sound.  A two channel signal put summed turns into mono.</p><pre><code>SinOsc([400, 660], 0).sum * 0.1
</code></pre><p>And then, of course, Pan2 allows me to place this in the stereo field:</p><pre><code>Pan2(SinOsc([400, 660], 0).sum, MouseX(-1, 1, 0, 0.2), 0.1)
</code></pre><p>You are now equipped to explore additive synthesis via sine tones.</p><p>In additive synthesis, if we know a recipe for the spectrum (frequency content) of a sound, we can synthesise it by adding up sine tones for each component frequency.</p><p>Recipes for common waveforms are known from the Fourier theory of sound (sinusoids at which frequencies and amplitudes to add up to create certain waveform shapes).</p><p>Sawtooth wave: Add up n harmonics with amplitude falling off as 1 / harmonicnumber, sign alternates between +1 and -1.</p><pre><code>var n = 10;
var wave = 0.to(9).collect({
    arg i;
    var mult = (-1 ** i) * (0.5 / (i + 1));
    SinOsc(440 * ( i + 1), 0) * mult
}).sum;
Pan2(wave / n, 0, 0.1) // stereo, panned centre
</code></pre><p>Square wave: Sum of odd harmonics, no even, amplitude falls as off 1/harmonicnumber; closest 'real' waveform is a clarinet tone</p><pre><code>var n = 10;
var wave = 0.to(9).collect({
    arg i;
    var harmonicnumber = 2 * i + 1; // odd harmonics only
    SinOsc(440 * harmonicnumber, 0) / harmonicnumber
}).sum;
Pan2(wave / n, 0, 0.1) // stereo, panned centre
</code></pre><p>Triangle wave: also odd harmonics only, falls off as 1 over harmonicnumber squared with alternating sign</p><pre><code>var n = 10;
var wave = 0.to(9).collect({
    arg i;
    var harmonicnumber = 2 * i + 1; // odd harmonics only
    var mult = (-1 ** ((harmonicnumber - 1) / 2)) * (1 / (harmonicnumber * harmonicnumber));
    SinOsc(440 * harmonicnumber, 0) * mult
}).sum;
Pan2(wave / n, 0, 0.1) // stereo, panned centre
</code></pre><p>Bell sound example:</p><pre><code>500 * [0.5, 1, 1.19, 1.56, 2, 2.51, 2.66, 3.01, 4.1]
</code></pre><p>This is a spectral recipe for a minor third bell, at a base frequency of 500 - run this line of code to see how the frequencies are calculated from the multipliers</p><pre><code>var partialRatios = [0.5, 1, 1.19, 1.56, 2, 2.51, 2.66, 3.01, 4.1];
SinOsc(500 * partialRatios, 0).sum * 0.05 // bell spectra, all partials the same volume
</code></pre><p>I can also give each partial its own amplitude in the mix, rather than defaulting them all to 0.1</p><pre><code>var partialRatios = [0.5, 1, 1.19, 1.56, 2, 2.51, 2.66, 3.01, 4.1];
var partialAmpl = [0.25, 1, 0.8, 0.5, 0.9, 0.4, 0.3, 0.6, 0.1];
(SinOsc(500 * partialRatios, 0) * partialAmpl).sum * 0.1 // bell spectra,  different volumes for partials
</code></pre><p>Here is a generalisable patch that uses the variable n to hold the number of sine tones desired for each run of the code:</p><pre><code>var n = 10;
SinOsc(250 * 1.to(n), 0) / n
</code></pre><p>If you're unsure what something is in code, investigate it in isolation.</p><p>There are lots of ways of dealing with arrays of data in SuperCollider, that we'll investigate as we go.</p><p>NC: <a href="https://composerprogrammer.com/teaching/supercollider/sctutorial/2.1%20Subtractive%20and%20Additive%20Synthesis.html">2.1</a></p>
