<h1>Cutoff - very simple envelope shape</h1><p><em>Cutoff(sustain, decay, curve)</em></p><p>Apply a cutoff envelope to a continuous sound.</p><ul><li>sustain: sustain portion of the envelope.</li><li>decay: decay portion of the envelope.</li><li>curve: shape of envelope segment.</li></ul><p>Cutoff sine oscillator after four seconds with one second release time:</p><pre><code>Cutoff(4, 1, -4) * SinOsc(220, 0) * 0.1
</code></pre><h1>Decay - exponential decay</h1><p><em>Decay(in, decayTime)</em></p><p>This is essentially the same as <em>Integrator</em> except that instead of supplying the coefficient directly, it is caculated from a 60 dB decay time. This is the time required for the integrator to lose 99.9 % of its value or -60dB. This is useful for exponential decaying envelopes triggered by impulses.</p><ul><li>in: input signal</li><li>decayTime: 60 dB decay time in seconds.</li></ul><p>Decay time of one centisecond:</p><pre><code>Decay(Impulse(1, 0) * 0.25, 0.01)
</code></pre><p>Used as an envelope:</p><pre><code>Decay(Impulse(XLn(1, 50, 20), 0) * 0.25, 0.2) * PinkNoise()
</code></pre><h1>Decay2 - exponential decay</h1><p><em>Decay2(in, attackTime, decayTime)</em></p><p><em>Decay</em> has a very sharp attack and can produce clicks. <em>Decay2</em> rounds off the attack by subtracting one Decay from another. <em>Decay2(in, attackTime, decayTime)</em> is equivalent to <em>Decay(in, decayTime) - Decay(in, attackTime)</em>.</p><ul><li>in: input signal</li><li>attackTime: 60 dB attack time in seconds.</li><li>decayTime: 60 dB decay time in seconds.</li></ul><p>One millisecond attack, one centisecond decay:</p><pre><code>Decay2(Impulse(1, 0), 0.001, 0.01)
</code></pre><p>Since attack and decay are a difference of two Decays, if you swap the values, then the envelope turns upside down:</p><pre><code>Decay2(Impulse(1, 0), 0.01, 0.001)
</code></pre><p>Used as an envelope:</p><pre><code>Decay2(Impulse(XLn(1, 50, 20), 0) * 0.25, 0.01, 0.2) * FSinOsc(600, 0)
</code></pre><p>Compare the above with Decay used as the envelope:</p><pre><code>Decay(Impulse(XLn(1, 50, 20), 0) * 0.25, 0.2) * FSinOsc(600, 0)
</code></pre><h1>DegreeToKey- convert signal to modal pitch</h1><p><em>DegreeToKey(table, in, octave)</em></p><p>The input signal value is truncated to an integer value and used as an index into an octave repeating table of note values. Indices wrap around the table and shift octaves as they do.</p><ul><li>table: an instance of FloatArray or Signal which contains the steps for each scale degree.</li><li>in: the input signal.</li><li>octave: the number of steps per octave in the scale. The default is 12.</li></ul><p>Modal space, mouse x controls discrete pitch in dorian mode:</p><pre><code>var b = #[0, 2, 3.2, 5, 7, 9, 10].asLocalBuf;
var k = DegreeToKey(b, MouseX(0, 15, 0, 0.1), 12);
var c = {
    arg n, r;
    var o = SinOsc((r + k + (n * 0.04)).midiCps, 0) * 0.1;
    var t = LFPulse(#[48, 55].midiCps, 0, 0.15);
    var f = (SinOsc(0.1, 0) * 10 + r).midiCps;
    var d = RLPF(t, f, 0.1) * 0.1;
    var m = o + d;
    CombN(m, 0.31, 0.31, 2) + m
};
var n = LFNoise1([3, 3]);
(c.value(n, 48) + c.value(n, 72)) * 0.25
</code></pre><h1>GetTempo - continuous tempo</h1><p><em>GetTempo()</em></p><p>Gives the current tempo as a control rate signal.  Tempo is always in beats per second.</p><pre><code>SetTempo(MouseX(0.25, 4, 1, 0.2))
</code></pre><p>Use the tempo to modulate an oscillator:</p><pre><code>SinOsc(400 * GetTempo(), 0, 0.2);
</code></pre><h1>Impulse - impulse oscillator</h1><p><em>Impulse(freq, phase)</em></p><p>Outputs non band limited single sample impulses.</p><ul><li>freq: frequency in Hertz</li><li>phase: initial phase</li></ul><p>Constant frequency:</p><pre><code>Impulse(800, 0) * 0.2
</code></pre><p>Modulate frequency:</p><pre><code>Impulse(XLn(800, 100, 5), 0) * 0.2
</code></pre><h1>Index - index into a table with a signal</h1><p><em>Index(table, in)</em></p><p>The input signal value is truncated to an integer value and used as an index into the table.  Out of range index values are clipped to the valid range.</p><ul><li>table: an instance of FloatArray or Signal.</li><li>in: the input signal.</li></ul><p>Index buffer for frequency values:</p><pre><code>var b = [50, 100, 200, 400, 800, 1600].asLocalBuf;
var f = Index(b, LinLin(LFSaw(2, 0), -1, 1, 0, 6));
SinOsc([f, f * 9], 0) * 0.1
</code></pre><h1>Integrator - leaky integrator</h1><p><em>Integrator(in, leak)</em></p><p>Integrates an input signal with a leak. The formula implemented is: <em>out(0) = in(0) + (leak * out(-1))</em></p><ul><li>in: input signal</li><li>leak: leak coefficient.</li></ul><p>As filter:</p><pre><code>Integrator(LFPulse(300, 0, 0.2) * 0.1, 0.9)
</code></pre><p>Used as an envelope:</p><pre><code>Integrator(LFPulse(3, 0, 0.2) * 0.0004, 0.999) * FSinOsc(700, 0)
</code></pre><h1>K2A - control rate to audio rate converter</h1><p><em>K2A(in)</em></p><p>Control rate signals are not legal outputs. If you want to output a control signal you need to convert it to audio rate. K2A converts via linear interpolation.</p><ul><li>in: input signal</li></ul><p>Control rate white noise interpolated to audio rate.</p><pre><code>K2A(WhiteNoise().kr * 0.2)
</code></pre><h1>Lag - exponential lag</h1><p><em>Lag(in, lagTime)</em></p><p>This is essentially the same as OnePole except that instead of supplying the coefficient directly, it is caculated from a 60 dB lag time. This is the time required for the filter to converge to within 0.01 % of a value. This is useful for smoothing out control signals.</p><ul><li>in: input signal</li><li>lagTime: 60 dB lag time in seconds.</li></ul><p>As filter:</p><pre><code>var x = LFPulse(300, 0, 0.5) * 0.1;
[x, Lag(x, 0.002)]
</code></pre><p>Used to lag pitch:</p><pre><code>SinOsc( // sine wave
  Lag( // lag the modulator
    LFPulse(4, 0, 0.5) * 50 + 400, // frequency modulator
    Ln(0, 1, 15) // modulate lag time
  ),
0) // phase
* 0.3 // sine amplitude
</code></pre><h1>Latch - sample and hold</h1><p><em>Latch(in, trig)</em></p><p>Holds input signal value when triggered.</p><ul><li>in: input signal.</li><li>trig: trigger. Trigger can be any signal. A trigger happens when the signal changes from non-positive to positive.</li></ul><p>Step noise:</p><pre><code>Latch(WhiteNoise(), Impulse(900, 0)) * 0.1
</code></pre><p>Step noise as frequency input:</p><pre><code>Blip(Latch(WhiteNoise(), Impulse(9, 0)) * 400 + 500, 4) * 0.1
</code></pre><p>The above are just meant as examples. LFNoise0 is a faster way to generate random steps:</p><pre><code>Blip(LFNoise0(9) * 400 + 500, 4) * 0.1
</code></pre><h1>LFPulse - pulse oscillator</h1><p><em>LFPulse(freq, phase, width)</em></p><p>A non-band-limited pulse oscillator. Outputs a high value of one and a low value of zero.</p><ul><li>freq: frequency in Hertz</li><li>width: pulse width duty cycle from zero to one.</li></ul><p>Fixed frequency:</p><pre><code>LFPulse(500, 0, 0.3) * 0.1
</code></pre><p>Used as both Oscillator and LFO:</p><pre><code>LFPulse(LFPulse(3, 0, 0.3) * 200 + 200, 0, 0.2) * 0.1
</code></pre><h1>LFSaw - sawtooth oscillator</h1><p><em>LFSaw(freq, phase)</em></p><p>A non-band-limited sawtooth oscillator. Output ranges from -1 to +1.</p><ul><li>freq: frequency in Hertz</li></ul><p>Fixed frequency:</p><pre><code>LFSaw(500, 0) * 0.1
</code></pre><p>Used as both Oscillator and LFO:</p><pre><code>LFSaw(LFSaw(4, 0) * 400 + 400, 0) * 0.1
</code></pre><h1>LFTri - triangle wave oscillator</h1><p><em>LFTri(freq, phase)</em></p><p>A non-band-limited triangle wave oscillator. Output ranges from -1 to +1.</p><ul><li>freq: frequency in Hertz</li></ul><p>Used as both Oscillator and LFO:</p><pre><code>LFTri(LFTri(1, 0) * 400 + 400, 0) * 0.1
</code></pre><h1>Ln - line generator</h1><p><em>Ln(start, end, dur)</em></p><p>Generates a line from the start value to the end value.</p><ul><li>start: starting value</li><li>end: ending value</li><li>dur: duration in seconds</li></ul><p>XLn is usually better than Line for frequency:</p><pre><code>SinOsc(Ln(200, 17000, 10), 0) * 0.1
</code></pre><h1>LinExp - convert a linear range to an exponential range</h1><p><em>LinExp(in, srclo, srchi, dstlo, dsthi)</em></p><p>Converts a linear range of values to an exponential range of values.  The dstlo and dsthi arguments must be nonzero and have the same sign.</p><ul><li>in: input to convert.</li><li>srclo: lower limit of input range.</li><li>srchi: upper limit of input range.</li><li>dstlo: lower limit of output range.</li><li>dsthi: upper limit of output range.</li></ul><p>Convert -1 to +1 sawtooth into 0.01 to 1.0 exponential:</p><pre><code>var s = LFSaw(500, 0);
[s * 0.1, LinExp(s, -1, 1, 0.001, 0.1)]
</code></pre><p>Convert oscillator output to frequency input:</p><pre><code>var mod = SinOsc(Line(1, 10, 10, 0), 0);
[SinOsc(mod * 400 + 500, 0), SinOsc(LinExp(mod, -1, 1, 100, 900), 0)] * 0.1
</code></pre><h1>Osc1 - one-shot oscillator</h1><p><em>Osc1(table, dur)</em></p><p>An oscillator that reads through a table only once.</p><ul><li>table: an instance of Signal; its size must be a power of 2.</li><li>dur: how long  to read through the table</li></ul><p>Pitch class table, linear interpolation, first slowly, then quickly, then slowly again:</p><pre><code>var tbl = [0, 2, 10, 12].asLocalBuf;
SinOsc((Osc1(tbl, 5) + 48).midiCps, 0) * 0.1
</code></pre><h1>Slew - slew rate limiter</h1><p><em>Slew(in, upSlope, downSlope)</em></p><p>Limits the slope of an input signal. The slope is expressed in units per second.</p><ul><li>in: input signal.</li><li>upSlope: maximum upward slope.</li><li>downSlope: maximum downward slope.</li></ul><p>Slew square wave:</p><pre><code>var z = LFPulse(800, 0, 0.5) * 0.1;
[z, Slew(z, 4000, 4000)]
</code></pre><h1>WrapIndex - index into a table with a signal</h1><p><em>WrapIndex(table, in)</em></p><p>The input signal value is truncated to an integer value and used as an index into the table.  Out of range index values are wrapped cyclically to the valid range.</p><ul><li>table: an instance of FloatArray or Signal.</li><li>in: the input signal.</li></ul><p>Indexing into a table:</p><pre><code>var buf = [200, 300, 400, 500, 600, 800].asLocalBuf;
SinOsc(WrapIndex(buf, MouseX(0, 6 * 3, 0, 0.1)), 0) * 0.1
</code></pre><h1>XLn - exponential line generator</h1><p><em>XLn(start, end, dur)</em></p><p>Generates an exponential curve from the start value to the end value. Both the start and end values must be non-zero and have the same sign.</p><ul><li>start: starting value</li><li>end: ending value</li><li>dur: duration in seconds</li></ul><p>Control frequency of sine oscillator:</p><pre><code>SinOsc(XLn(200, 17000, 10), 0) * 0.1
</code></pre>
